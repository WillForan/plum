#!/usr/bin/env perl
use strict; use warnings;
use v5.28;
use feature qw(signatures);
use File::Spec;
no warnings qw(experimental::signatures experimental::smartmatch);

# quick print of whats going on
our $DEBUG=0;
sub debug($msg, $level) { say "DEBUG($level): $msg" if $DEBUG >= $level; }

#
# Parse psuedo-plumb(7) (plan9port) plumber files
# requires ps, xdotool, xclip
#

# e.g.
#
#  # open http or https in a browser
#  text matches ^https?://
#  start xdg-open $text
#
#  # use ctrl+c clipboard to convert images when in inkscape
#  from secondary
#  text matches ^(\s+).(jpe?g|gif|ppm)$
#  add base=$1 ext=$2
#  app matches inkscape
#  start convert $text $base.png

####
# format: paragraphs (2x\n delimited) with
#
#  {text,cwd,app,title} matches REGEXP
#  add myvar=$1
#  plum PROGRAM $cwd $myvar 

# where
# * 'text' is the text on the clipboard
# * 'cwd' is current working directory
# * 'app' is the application focused
# and keywords 
#  * from      -- which clipboard
#  * matches   -- regexp
#  * add       -- create variable
#  * start     -- launch program (from cwd of X11 app)


# find what it is we are told to (file or folder)
# are there other objects? should we abstract this more?
sub find_path($search, $textInfo) {
   my $cwd=($textInfo->{cwd}||"./");
   $cwd =  File::Spec->canonpath($cwd); 
   #debug(Dumper($cwd_rules),4);
   debug("path testing w/cwd: $cwd", 3);
   for my $f ($search, "$cwd/$search") {
      debug("testing path: $f", 4);
      return($f) if( -e -r $f);
      # try glob
      my @g = glob($f);
      return($g[0]) if($#g == 0 && -e -r $g[0]);
   }
   # nothing matches
   return("");
}

# TODO: how do we call '-f' or '-d' as a variable? 
sub isfile($search,$textInfo) {
  my $f=find_path($search,$textInfo);
  return ( $f ne "" && -f $f)?$f:"";
}
sub isdir($search, $textInfo) {
  my $f=find_path($search,$textInfo);
  return ( $f ne "" && -d $f)?$f:"";
}

# functions get called by looking up from matched string
our %loc = ( 'file' => \&isfile, 'dir' => \&isdir);


sub cmd_from_section($INFH, $textInfo) {
   my @matches = (); # reg exp matches
   my $cmd = "";
   while($_=<$INFH>) {
      chomp;
      debug("section readline: $_",3);
      s/ #.*//;
      
      given($_) {
         when(/^\s*#/) {
            debug("skipping comment $_", 1);
            next;
         }
         when(m/^from (\w+)/) {
            $textInfo->{'text'}=`xclip -o -selection $1`;
         }
         when(m/^(text|cwd|app) matches (.*)/) {
            debug("matching $2 against $1: $textInfo->{$1}", 1);
            my $re = qr/$2/;
            @matches = ($textInfo->{$1} =~ /$re/);
            # if no matches, we dont do anything
            debug("no matches '$_'", 1) if $#matches < 0;
            last if $#matches < 0;
         }
         when(m/^add (.*)/){
            while( m/(\w+)=\$(\d+)/g) {
               $textInfo->{$1} = $matches[$2-1];
               debug("added $1 = ". $matches[$2-1],1);
            }
         }
         # add file or dir to the textInfo hash or rule doesn't match
         # isfile and isdir (future maybe isgit isman ?)
         when(m/^arg is(file|dir) (.*)/){
            my $f_or_d=$1;
            my $totest=$2;
            $totest =~ s/\$(\d+)/$matches[$1-1]/ge; # replace eg $1
            $totest =~ s/\$(\w+)/$textInfo->{$1}/ge;# replace eg $mfile

            debug("testing $f_or_d: $totest", 3);
            $textInfo->{$f_or_d} = $loc{$f_or_d}->($totest, $textInfo);
            # if lookup failed, get out of here
            last if ! $textInfo->{$f_or_d};
            debug("added $f_or_d = $totest ($textInfo->{$f_or_d})", 1);
         }
         # replace command with anything we've stored
         when(m/^start (.*)/){
            $cmd="$1";
            $cmd =~ s/\$(\d+)/$matches[$1-1]/ge; # replace eg $1
            $cmd =~ s/\$(\w+)/$textInfo->{$1}/ge; # replace eg $file
         }
         when(m/^$/){
            last;
         }
         default {
            debug("section: line not understood: $_",0);
            last
         }
      }
  }
     
  # read up to the next section
  until(!$_ or /^$/){ $_=<$INFH>; }
  #until(/^$/ or eof($INFH)){ $_=<$INFH>; } 
  # inline::files eof() not supported
  return $cmd;
}

# get title, app, and clipboard text
sub setup_info() {
   my $textInfo = {};
   # xdotool to get pid
   chomp(my $wpid=`xdotool getwindowfocus getwindowpid`);
   debug("wpid: $wpid",1);

   # grab last entry from ps, take only the first word
   chomp(my $psapp = (split(/\n/, `ps -o comm,args -p $wpid`))[-1]);
	$textInfo->{app}=(split(/\s+/, $psapp))[0];
   debug("app: $textInfo->{app}",1);

   # use xdotool to get window
   chomp($textInfo->{title} = `xdotool getactivewindow getwindowname`);
   debug("title: $textInfo->{title}",1);

   # fetch clipboard
   # TODO: use perl lib for cross platform? will require cpanm install
   $textInfo->{text} = `xclip -o`;
   debug("text: $textInfo->{text}",1);
   return($textInfo);
}

# get rules for extracting cwd from title
# N.B. ugly hack to rewind when we find non-matching line
sub cwd_rules($INFH){
   my $prev_pos=tell $INFH;
   my $cwd_rules={};
   while($_=<$INFH>){
      debug("cwd_rules readline: $_",3);
      next if m/^\s*#/; # okay not to tell pos
      if( m/parse cwd (\S+) (\S+) (\S+)/){
         $prev_pos=tell $INFH;
         $cwd_rules->{$1} = {patt=>$2, replace=>$3};
         debug("added cwd rule for '$1': '$cwd_rules->{$1}->{patt}' => '$cwd_rules->{$1}->{replace}'", 1);
         next;
      } else {
         debug("done with 'parse cwd'", 3);
         seek $INFH, $prev_pos, 0;
         last;
      }
   }
   return($cwd_rules);
}

sub get_cwd($cwd_rules, $app, $title){
   # interate through each regex matching an application name
   foreach my $k (keys %{$cwd_rules}){
      debug("trying cwd rule $k", 4);
      # test app against regexp (also key for cwd_rules)
      my $app_re = qr/$k/;
      next unless($app =~ /$app_re/);

      debug("rule matched, trying $title against $cwd_rules->{$k}->{patt}", 4);
      # if app matches, test the title pattern (and replace)
      my $title_re = qr/$cwd_rules->{$k}->{patt}/;
      next unless $title =~ s/.*$title_re.*/$cwd_rules->{$k}->{replace}/ee;

      debug("found cwd in title: $title", 3);
      return($title); # subsitution worked, title is now cwd
   }
   return("");
   # TODO: lsof cwd? not often correct
}

#
# 
sub read_config($INFH, $textInfo){
   my $cwd_rules = cwd_rules($INFH);
   $textInfo->{cwd} = get_cwd($cwd_rules, $textInfo->{app}, $textInfo->{title});
   debug("read cwd as $textInfo->{cwd}",3);

   my $cmd = "";
   while(!eof($INFH)){
      # define ways to parse cwd given program regexp, title regexp, reconstruction
      $cmd=cmd_from_section($INFH, $textInfo);
      # read read of lines from failed command parse
      return($cmd) if $cmd;
   };
   # empty string if no command found
   return("")
}

# protect execution from test code
# -- only run if e.g. ./plum
unless(caller){
   my $textInfo = setup_info();
   my $fh;
   use File::Basename;
   my @conf_locs = ("$ENV{HOME}/.config/plum.conf",
      "/etc/plum.conf",
      dirname(__FILE__)."/plum.conf");
   foreach my $f (@conf_locs) {
      debug("testing $f",3);
      next unless -e $f;
      open($fh, '<', $f) or die "cannot open $f";
      debug("using $f",1);
   }
   if(not $fh){
      # die "cannot find config file!";
      $fh=*DATA if not $fh;
      debug("using built in plumbing",1);
   }

   my $cmd=read_config($fh, $textInfo);
   if($cmd eq ""){
      debug("no matching commands for '$textInfo->{text}'",1);
      exit(1);
   }

   debug("exec $cmd",3);
   exec $cmd;
}

__DATA__
######
#
# create config like:
#  sed -n '/__DATA__/,$p' plum|sed 1d > plum.conf
#
######

## setup how to find cwd from different titles
# extract from urxvt title
#   user@host:/path/to/cwd
# or tmux title
#   tmux:0:zsh - "user@host: ~/src/thing" 
parse cwd urxvt|term \w+@\S+?:\s?([~/]\S*?)"?(\s|$) $1

## what to run with what matches
# file.pl line 20, near "blah blah"
text matches (\S+) line (\d+), near "(.*)"
add file=$1 line=$2 search=$3
arg isfile $file
start vimit "$file" +$line +/"$search"

# file.pl line 20
text matches (\S+) line (\d+)
add file=$1 line=$2 
arg isfile $file
start vimit "$file" +$line

# example using clipboard as well as selection
# also useful for testing
text matches ^notify$
from clipboard
start xmessage "$text"

